buildscript {
    ext {
        repos = {
            gradlePluginPortal()
            mavenLocal()
            mavenCentral()
        }
        lombokVersion = '1.18.12'
        springBootVersion = '2.2.6.RELEASE'
        springCloudVersion = '2.2.2.RELEASE'
        feignVersion = '10.7.4'
        swaggerVersion = '2.9.2'
        jacksonDatabindVersion = '2.10.3'
        pitestVersion = '1.4.8'

        repositories(repos)
    }
}


plugins {
    id 'groovy'
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version "$springBootVersion"
    id 'info.solidsoft.pitest' version "$pitestVersion"
}

apply from: 'gradle/test.gradle'
apply from: 'gradle/version.gradle'

group 'com.trzewik'
version gitVersion()

sourceCompatibility = 1.8

repositories(repos)

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign:${springCloudVersion}"
    implementation "io.github.openfeign:feign-httpclient:${feignVersion}"   //required to use PATCH method

    implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonDatabindVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonDatabindVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

bootJar {
    mainClassName = 'com.trzewik.information.consumer.App'
    manifest {
        attributes 'Start-Class': 'com.trzewik.information.consumer.App'
    }
    launchScript()
}

bootRun {
    main = 'com.trzewik.information.consumer.App'
}

pitest {
    useClasspathFile = true
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}
